plugins {
    id 'org.jetbrains.kotlin.jvm' version '1.3.72'
    id 'application'
}

group 'org.example'
version '1.0-SNAPSHOT'

repositories {
    mavenCentral()
}

mainClassName = "virtMem.VirtualMemoryKt"

sourceSets {
    main {
        kotlin {
            srcDirs = ['src']
        }
    }
    test {
        kotlin {
            srcDirs = ['test']
        }
    }
    integTest {
        compileClasspath += sourceSets.main.output
        runtimeClasspath += sourceSets.main.output
        java {
            srcDirs = ['integrationTests']
        }
    }
}

configurations {
    integTestImplementation.extendsFrom implementation
    integTestRuntimeOnly.extendsFrom runtimeOnly
}

jar {
    manifest {
        attributes(
                'Main-Class': 'virtMem.VirtualMemoryKt'
        )
    }
    from {
        configurations.runtimeClasspath.collect {
            it.isDirectory()? it: zipTree(it)
        }
    }
}

dependencies {
    implementation "org.jetbrains.kotlin:kotlin-stdlib"
    testImplementation "org.junit.jupiter:junit-jupiter-api:5.4.2"
    testRuntimeOnly "org.junit.jupiter:junit-jupiter-engine:5.4.2"
    integTestImplementation "org.jetbrains.kotlin:kotlin-test:1.4.10"
    integTestImplementation "org.junit.jupiter:junit-jupiter-api:5.4.2"
    integTestRuntimeOnly "org.junit.jupiter:junit-jupiter-engine:5.4.2"
}

task integTest(type: Test){
    testClassesDirs = sourceSets.integTest.output.classesDirs
    classpath = sourceSets.integTest.runtimeClasspath
    useJUnitPlatform()
}

test {
    useJUnitPlatform()
}